var 
  makeCoord2D = f(x, y) => 
  {
    var xcoord = x, ycoord = y in 
    dispatch(todo, arg) => 
    {
      if (todo = "getX") xcoord
      else if (todo = "getY") ycoord
      else if (todo = "setX") xcoord := arg 
      else if (todo = "setY") ycoord := arg 
      else unit
    } 
  } 
in

var
  pointA = makeCoord2D(5, 6),
  pointB = makeCoord2D(7, 9)
in

pointA("setX", 2);
pointB("setY", pointA("getX", unit));

output pointA("getX", unit);
output pointA("getY", unit);
output pointB("getX", unit);
output pointB("getY", unit);

var makeCoord3D = f(x, y, z) => 
{
  var parent = makeCoord2D(x, y), zcoord = z in 
  dispatch(todo, arg) =>
  {
    if (todo = "getZ") zcoord
    else if (todo = "setZ") zcoord := arg 
    else parent(todo, arg)
  } 
}
in

var
  pointC = makeCoord3D(1, 2, 3),
  pointD = makeCoord3D(4, 5, 6)
in

pointC("setY", 12);
pointD("setZ", pointC("getY", unit));

output pointC("getX", unit); 
output pointC("getY", unit);
output pointC("getZ", unit);
output pointD("getX", unit); 
output pointD("getY", unit);
output pointD("getZ", unit)
